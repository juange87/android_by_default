apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

//apply from: '../config/quality/quality.gradle'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
}

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["androidCompileSdkVersion"]

    defaultConfig {
        minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
        targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")

        applicationId 'com.juange.androidbydefault'
        versionCode globalConfiguration["androidVersionCode"]
        versionName globalConfiguration["androidVersionName"]
        testInstrumentationRunner globalConfiguration["testInstrumentationRunner"]
        testApplicationId globalConfiguration["testApplicationId"]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        // You must set up an environment var before release signing
        // Run: export APP_KEY={password}
        // TODO Add your release keystore in /keystore folder
        release {
            storeFile file('keystore/release.keystore')
            keyAlias 'alias'
            storePassword "$System.env.APP_KEY"
            keyPassword "$System.env.APP_KEY"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            versionNameSuffix " Debug"
            debuggable true
        }
    }

    //Needed because of this https://github.com/square/okio/issues/58
    lintOptions {
        warning 'InvalidPackage'

        abortOnError false

        htmlReport true
        // optional path to report (default will be lint-results.html in the build dir)
        htmlOutput file("$project.buildDir/reports/lint/lint.html")
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    def dependencies = rootProject.ext

    //KOTLIN
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //DAGGER
    annotationProcessor "com.google.dagger:dagger-compiler:${dependencies.daggerVersion}"
    implementation "com.google.dagger:dagger:${dependencies.daggerVersion}"

    //Android
    implementation "com.android.support:design:${dependencies.supportLibsVersion}"
    implementation "com.android.support:appcompat-v7:${dependencies.supportLibsVersion}"
    compileOnly "javax.annotation:jsr250-api:${dependencies.javaxAnnotationVersion}"
    implementation "com.android.support:recyclerview-v7:${dependencies.recyclerViewVersion}"

    //RxJava
    implementation "io.reactivex.rxjava2:rxjava:${dependencies.rxJavaVersion}"
    implementation "io.reactivex.rxjava2:rxandroid:${dependencies.rxAndroidVersion}"

    // Unit tests dependencies
    testImplementation "junit:junit:${dependencies.jUnitVersion}"
    testImplementation "org.mockito:mockito-core:${dependencies.mockitoVersion}"

    //UI Tests Dependencies
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:${dependencies.daggerVersion}"
    androidTestImplementation "com.google.dagger:dagger:${dependencies.daggerVersion}"
    androidTestImplementation "org.mockito:mockito-core:${dependencies.mockitoVersion}"
    androidTestImplementation "com.github.fabioCollini:DaggerMock:${dependencies.daggerMockVersion}"

    androidTestImplementation("com.android.support.test.espresso:espresso-core:${dependencies.espressoVersion}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation("com.android.support.test:runner:${dependencies.testRunnerVersion}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation("com.android.support.test:rules:${dependencies.testRulesVersion}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestImplementation "com.google.dexmaker:dexmaker:${dependencies.dexMakerVersion}"
    androidTestImplementation "com.google.dexmaker:dexmaker-mockito:${dependencies.dexMakerVersion}"
}
